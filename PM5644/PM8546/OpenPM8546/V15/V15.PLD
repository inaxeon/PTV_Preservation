Name     V15 ;
PartNo   00 ;
Date     26/09/2023 ;
Revision 01 ;
Designer Matt Millman ;
Company  - ;
Assembly None ;
Location  ;
Device   g22v10 ;

/*
 *   Philips PM8546 Logo Generator
 *   Open source recreation
 * 
 *   File        : V15.pld
 *   Author      : Matt Millman
 *   Description : Horizontal counter PAL
 *
 *   This is free software: you can redistribute it and/or modify
 *   it under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 2 of the License, or
 *   (at your option) any later version.
 *   This software is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *   You should have received a copy of the GNU General Public License
 *   along with this software.  If not, see <http://www.gnu.org/licenses/>.
 */

/* &=AND #=OR $=XOR */

PIN 1 = GCLK;
PIN 2 = LCNT_IN; /* Output of 'HC161/AC00 */
PIN 3 = nFH_IN; /* Horizontal sync */
PIN [4..11] = [SH0..7];
PIN 13 = SH8;
PIN 14 = nSHIFT_LOAD;
PIN 15 = H2;
PIN 16 = H4;
PIN 17 = H6;
PIN 18 = H8;
PIN 19 = H7;
PIN 20 = H5;
PIN 21 = H3;
PIN 22 = H1;
PIN 23 = H0;

H0.oe = 'b'1;
H1.oe = 'b'1;
H2.oe = 'b'1;
H3.oe = 'b'1;
H4.oe = 'b'1;
H5.oe = 'b'1;
H6.oe = 'b'1;
H7.oe = 'b'1;
H8.oe = 'b'1;

H0.sp = 'b'0;
H1.sp = 'b'0;
H2.sp = 'b'0;
H3.sp = 'b'0;
H4.sp = 'b'0;
H5.sp = 'b'0;
H6.sp = 'b'0;
H7.sp = 'b'0;
H8.sp = 'b'0;

H0.ar = 'b'0;
H1.ar = 'b'0;
H2.ar = 'b'0;
H3.ar = 'b'0;
H4.ar = 'b'0;
H5.ar = 'b'0;
H6.ar = 'b'0;
H7.ar = 'b'0;
H8.ar = 'b'0;

H0.d = ((nSHIFT_LOAD $ !H0) & nFH_IN) $ (!nFH_IN & SH0);
H1.d = ((H1 $ (H0 & !nSHIFT_LOAD)) & nFH_IN) $ (!nFH_IN & SH1);
H2.d = ((H2 $ (H1 & H0 & !nSHIFT_LOAD)) & nFH_IN) $ (!nFH_IN & SH2);
H3.d = ((H3 $ (H2 & H1 & H0 & !nSHIFT_LOAD)) & nFH_IN) $ (!nFH_IN & SH3);
H4.d = ((H4 $ (H3 & H2 & H1 & H0 & !nSHIFT_LOAD)) & nFH_IN) $ (!nFH_IN & SH4);
H5.d = ((H5 $ (H4 & H3 & H2 & H1 & H0 & !nSHIFT_LOAD)) & nFH_IN) $ (!nFH_IN & SH5);
H6.d = ((H6 $ (H5 & H4 & H3 & H2 & H1 & H0 & !nSHIFT_LOAD)) & nFH_IN) $ (!nFH_IN & SH6);
H7.d = ((H7 $ (H6 & H5 & H4 & H3 & H2 & H1 & H0 & !nSHIFT_LOAD)) & nFH_IN) $ (!nFH_IN & SH7);
H8.d = ((H8 $ (H7 & H6 & H5 & H4 & H3 & H2 & H1 & H0 & !nSHIFT_LOAD)) & nFH_IN) $ (!nFH_IN & SH8);

nSHIFT_LOAD.sp = 'b'0;
nSHIFT_LOAD.ar = 'b'0;
nSHIFT_LOAD.oe = 'b'1;
nSHIFT_LOAD.d = LCNT_IN # !nSHIFT_LOAD;